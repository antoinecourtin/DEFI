# Séance du 20191106
## Corrections exercices

### 1er: juste les id peintures de monet 
```sparql
SELECT ?item 
WHERE
{
  ?item wdt:P31 wd:Q3305213 .
  ?item wdt:P170 wd:Q296 .
}
```

### 2ème: avec les labels (via le service wikibase:label) et les images associées
```sparql
SELECT ?item ?itemLabel ?pic ?CollectionLabel
WHERE
{
  ?item wdt:P31 wd:Q3305213 .
  ?item wdt:P170 wd:Q296 .
  ?item wdt:P18 ?pic .
  SERVICE wikibase:label { 
bd:serviceParam wikibase:language "fr,en"}
}
```

### 3ème: avec en option (via OPTIONAL) les collections/lieux de conservation
```sparql
SELECT ?item ?itemLabel ?pic ?CollectionLabel
WHERE
{
  ?item wdt:P31 wd:Q3305213 .
  ?item wdt:P170 wd:Q296 .
  ?item wdt:P18 ?pic .
  SERVICE wikibase:label { 
bd:serviceParam wikibase:language "fr,en"}
  OPTIONAL { 
  ?item wdt:P195 ?Collection
    }
}
```
### 4ème: compter le nombre de Monet dans chaque collection/lieux de conservation et les afficher par odre décroissant
```sparql
SELECT ?CollectionLabel (COUNT(?item) AS ?count)
WHERE
{
  ?item wdt:P31 wd:Q3305213 .
  ?item wdt:P170 wd:Q296 .
  ?item wdt:P18 ?pic .
  SERVICE wikibase:label { 
bd:serviceParam wikibase:language "fr,en"}
  OPTIONAL { 
  ?item wdt:P195 ?Collection
    }
}
GROUP BY ?CollectionLabel
ORDER BY DESC (?count)
```

#### Afficher sous forme de grille d’images, les tableaux qui dépeignent un lion.

````sparql
#defaultView:ImageGrid

SELECT DISTINCT ?item ?image
WHERE
{
  ?item wdt:P31/wdt:P279* wd:Q3305213 .  
  ?item wdt:P180 ?object .
 ?object wdt:P279* wd:Q140 . 
  ?item wdt:P18 ?image .
}
````

#### Nom de 100 villes dont la population dépasse le 1 millions de personnes.

````sparql
#Noms de 100 villes de plus de 1 000 000 d’habitants
#added before 2016-10

SELECT ?city ?cityLabel ?population
WHERE
{
  ?city wdt:P1082 ?population . 
  FILTER(?population>1000000)
  SERVICE wikibase:label { bd:serviceParam wikibase:language "fr,en". }
} LIMIT 100
````

#### Afficher sur une carte, tous les lieux où l’on conserve des peintures en indiquant pour chaque lieu le nombre de peinture.

````
#Carte des tableaux dont on connait une localisation avec le nombre de tableaux pour le lieu
#defaultView:Map
SELECT ?locationLabel ?coord (count(*) as ?count)
WHERE
{
    ?painting wdt:P31 wd:Q3305213 .
    ?painting wdt:P276 ?location .
	?location wdt:P625 ?coord
	SERVICE wikibase:label { bd:serviceParam wikibase:language "en" }      
}
GROUP BY ?locationLabel ?coord
LIMIT 100

````


## Palladio
* pour accéder à l'application : http://hdlab.stanford.edu/palladio-app/#/upload
* exemple d'URL Tiles `https://mapwarper.net/maps/tile/14405/{z}/{x}/{y}.png` + `https://mapwarper.net/maps/tile/20031/{z}/{x}/{y}.png`
* jeux de tests :
	* le paquet json : data_RETIF_HdF_2017_all_Palladio (1).json
	* un fichier csv : Export_EnvoisdeRome_oeuvres_20190325.csv
* URL du SPARLEndpoint de Wikidata pour utiliser Palladio
https://query.wikidata.org/bigdata/namespace/wdq/sparql
* exemple de requête que l'on peut jouer de wikidata dans Palladio : 
````sparql
SELECT distinct ?creator ?creatorLabel ?datebirth ?datedeath ?placebirthLabel (CONCAT(STR(?lat),", ",STR(?long)) as ?lat_long) ?placedeathLabel (CONCAT(STR(?lat_2),", ", STR(?long_2)) as ?lat_long_2)

WHERE {
?creator wdt:P106 wd:Q1028181;
          p:P570/psv:P570 [
            wikibase:timeValue ?date;
            wikibase:timePrecision ?precision
          ].
  FILTER("1500-12-31"^^xsd:dateTime < ?date && ?date < "1600-00-00"^^xsd:dateTime).
  FILTER(?precision >= 9).
  BIND(YEAR(?date) AS ?year).
?creator wdt:P19 ?placebirth. # lieu de naissance 
?creator wdt:P569 ?datebirth.
?creator wdt:P570 ?datedeath. 
?placebirth wdt:P625 ?coord.
?placebirth p:P625 ?declaration.
?declaration psv:P625 ?coord_geo.
?coord_geo wikibase:geoLatitude ?lat.
?coord_geo wikibase:geoLongitude ?long.
?creator wdt:P20? ?placedeath. 
?placedeath wdt:P625 ?coord_death.
?placedeath p:P625 ?declaration2.
?declaration2 psv:P625 ?coord_geo_2.
?coord_geo_2 wikibase:geoLatitude ?lat_2.
?coord_geo_2 wikibase:geoLongitude ?long_2.
SERVICE wikibase:label { 
bd:serviceParam wikibase:language "fr,en"}
}

http://tinyurl.com/ybrcyhb8
````

